// A ~realistic schema that demonstrates firemodel v0.2 syntax using metaphors from instagram.

// ---------------------
// [new] ASCII ART FTW!
// ---------------------

// [new] Interfaces allow you to declare structural abstractions. If a model or struct declares that it
// implements an interface, it must include all the interface's fields with correct types.
//
// Interfaces are intended for compostability and abstraction, similar to graphql interfaces.
interface HasToken {
  string token;
}

// [unchanged] Structs are used for sharing structures of nested content, which
// are stored as Maps in firestore and also accessible via FieldPaths in most
// firestore client libraries.
struct Avatar {
  URL url;
  string color;
}

// This is the service. There can only be one.
service {
  rpc SendMessage(SendMessageRequest);
}

struct SendMessageRequest {
  reference<Friend> to;
  MessageContent content;
}

// Models define structures for firestore documents models. Models cannot be used as field types.
// [new] Models can implement zero or more interfaces.
// [new] Models can have nested models, which designate nested collections.
// [new] Models use an inflector to determine their pluralized collection name in firestore.
// [new] It is now assumed that all fields of all types (other than enums associated values) are optional. In order to facilitate queries on missing fields, missing fields are saved in firestore as explicit null values.
model User: "/users/{user_id}" implements HasToken, Author {
  string username;
  string display_name;
  Avatar avatar;
}

model Gram: "/grams/{gram_id}" {
  Audience shared_with;
  URL photo_url;
  string description;
  array<string> tags;
}

model Message: "/messages/{message_id}" {
  MessageContent content;
  reference<Friend> from;

}

model Attachment: "/attachments/{attachment_id}" {
  string title;
  AttachmentContent content;
}

model Friend: "/users/{user_id}/friends/{friend_id}" implements Author {
  string username;
  string display_name;
  Avatar avatar;
  timestamp friends_sinice;
}

// [unchanged] Enums provide type safety around string enumerations. Enums are stored in firestore as capitalized strings.
enum Audience {
  global,
  friends,
}

// [new] Enums values may now optionally include associated values. Associated
// values must have a struct type. Associated values are stored in firestore
// under a period-delimited key, prefixed with the enum field name. The enum
// case is always written, even when there is also an associated value. Keys
// for enum values other than the active one are not written to firestore.
enum AttachmentContent {
  // e.g. for an Attachment, written as `content = "PLACEHOLDER"`
  placeholder,
   // e.g. for an Attachment, written as `content = "IMOJI", content.imoji = #BYTES#`
  imoji(ImojiAttachment),
   // e.g. for an Attachment, written as `content = "IMOJI", content.imoji = #BYTES#`
  gram(GramAttachment),
   // e.g. for an Attachment, written as `content = "UPLOAD", content.upload.token = "oijasdf", content.upload.etc = the_rest`
  upload(UploadAttachment),
}

struct ImojiAttachment {
    bytes cool_pic;
}

struct GramAttachment {
    string ref;
}

struct UploadAttachment {
    string title;
    URL src;
}

struct Upload {
  URL url;
  string mime_type;
}

enum MessageContent {
  text(TextMessageContent),
  photo(PhotoMessageContent),
}

struct TextMessageContent {
  string message;
}

struct PhotoMessageContent {
  string caption;
  URL url;
}

interface Author {
  string username;
  string display_name;
  Avatar avatar;
}
